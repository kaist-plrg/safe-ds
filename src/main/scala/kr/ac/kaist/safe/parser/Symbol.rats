/**
 * *****************************************************************************
 * Copyright (c) 2016-2018, KAIST.
 * All rights reserved.
 *
 * Use is subject to license terms.
 *
 * This distribution may include materials developed by third parties.
 * ****************************************************************************
 */

/*
 * Definition of JavaScript symbols and operators.
 */
module kr.ac.kaist.safe.parser.Symbol(Identifier, Spacing, Keyword);

import Identifier;
import Spacing;
import Keyword;

option setOfString(JAVASCRIPT_RESERVED);

transient void commaOrNot = "," / w ;
transient void semicolon = ";";
transient void colon = ":";
transient void underscore = "_";

transient void openparen = "(";
transient void closeparen = ")";
transient void opencurly = "{";
transient void closecurly = "}";
transient void opensquare = "[";
transient void closesquare = "]";
transient void openangle = "<";
transient void closeangle = ">";

transient String dot   = ".";
transient String comma = ",";
transient String star = "*";
transient String question = "?";

Op and = "&" !("&" / "=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "&"); };
Op ands = "&&"
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "&&"); };
Op bar = "|" !("|" / "=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "|"); };
Op bars = "||"
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "||"); };
Op caret = "^" !("=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "^"); };

Op equals = "=" !("=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "="); };
Op doubleplus = "++"
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "++"); };
Op doubleminus = "--"
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "--"); };
Op plus = "+" !("+" / "=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "+"); };
Op minus = "-" !("-" / "=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "-"); };
Op tilde = "~"
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "~"); };
Op bang = "!"
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), "!"); };

/*
 * AssignmentOp ::= *= | /= | %= | += | -= | <<= | >>= | >>>= | &= | ^= | |=
 */
Op AssignmentOp =
     a1:("*" / "/" / "%" / "+" / "-" / "<<" / ">>>" / ">>" / "&" / "^" / "|") equals
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), a1+"="); };

/*
 * EqualityOp ::= == | != | === | !==
 */
Op EqualityOp =
     a1:("===" / "!==" / "==" / "!=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), a1); };

/*
 * RelationalOp ::= < | > | <= | >= | instanceof | in
 */
Op RelationalOp =
     a1:("<=" / ">=" / "<" / ">" / instanceof / in)
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), a1); };

/*
 * RelationalOpNoIn ::= < | > | <= | >= | instanceof
 */
Op RelationalOpNoIn =
     a1:("<=" / ">=" / "<" / ">" / instanceof)
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), a1); };

/*
 * ShiftOp ::= << | >> | >>>
 */
Op ShiftOp =
     a1:("<<" / ">>>" / ">>") !("=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), a1); };

/*
 * AdditiveOp ::= + | -
 */
Op AdditiveOp =
     yyValue:(plus / minus);

/*
 * MultiplicativeOp ::= * | / | %
 */
Op MultiplicativeOp =
     a1:("*" / "/" / "%") !("=")
     { yyValue = new Op(NodeUtil.makeASTNodeInfo(createSpan(yyStart, yyCount)), a1); };
